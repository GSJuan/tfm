root ::= line
line ::= atom ( chain | branch )*
chain ::= ( dot atom | bond? ( atom | rnum) )+
branch ::= "(" ( ( bond | dot )? line )+ ")"
atom ::= organic_symbol | bracket_atom
bracket_atom ::= "[" isotope? symbol chiral? hcount? charge? | map? "]"
rnum ::= digit | "%" digit digit
isotope ::= digit? digit? digit
hcount ::= "H" digit?
charge ::= "+" ( "+" | fifteen )? | "-" ( "-" | fifteen )?
map ::= ":" digit? digit? digit
symbol ::= "A" ( "c" | "g" | "l" | "m" | "r" | "s" | "t" | "u" ) |
                   "B" ( "a" | "e" | "h" | "i" | "k" | "r" )? |
                   "C" ( "a" | "d" | "e" | "f" | "l" | "m" | "n" | "o" | "r"  "s" | "u" )? |
                   "D" ( "b" | "s" | "y" ) |
                   "E" ( "r" | "s" | "u" ) |
                   "F" ( "e" | "l" | "m" | "r" )? |
                   "G" ( "a" | "d" | "e" ) |
                   "H" ( "e" | "f" | "g" | "o" | "s" )? |
                   "I" ( "n" | "r" )? |
                   "K" "r"?  |
                   "L" ( "a" | "i" | "r" | "u" | "v" ) |
                   "M" ( "c" | "g" | "n" | "o" | "t" ) |
                   "N" ( "a" | "b" | "d" | "e" | "h" | "i" | "o" | "p" )? |
                   "O" ( "g" | "s" )? |
                   "P" ( "a" | "b" | "d" | "m" | "o" | "r" | "t" | "u" )? |
                   "R" ( "a" | "b" | "e" | "f" | "g" | "h" | "n" | "u" ) |
                   "S" ( "b" | "c" | "e" | "g" | "i" | "m" | "n" | "r" )? |
                   "T" ( "a" | "b" | "c" | "e" | "h" | "i" | "l" | "m" | "s" ) |
                   "U" | "V" | "W" | "Xe" | "Y" "b"? |
                   "Z" ( "n" | "r" ) |
                   "b" | "c" | "n" | "o" | "p" | "s" "e"? | "as"
organic_symbol ::= "B" "r"? | "C" "l"? | "N" | "O" | "P" | "S" | "F" | "I" | "At" | "Ts" | "b" | "c" | "n" | "o" | "p" | "s"
bond           ::= "-" | "=" | "#" | "$" | "/" | "\\"
dot            ::= "."
chiral         ::= "@"? "@"
digit          ::= "0" | "1" | "2" | "3" | "4" | "5" | "6" | "7" | "8" | "9"
fifteen        ::= "1" ("0" | "1" | "2" | "3" | "4" | "5")? | ( "2" | "3" | "4" | "5" | "6" | "7" | "8" | "9" )